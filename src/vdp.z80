;I/O ports
VDP_VRAM equ 0x0098
VDP_REG equ 0x0099

vdp_init:
	ld a,0b00000010		;setting screen mode 2
	ld b,0
	call vdp_RegWrite
	
	ld a,0b01100010		;setting various configurations
	ld b,1
	call vdp_RegWrite

	ld a,0b00000110		;name table address
	ld b,2
	call vdp_RegWrite

	ld a,0b11111111		;color table address
	ld b,3
	call vdp_RegWrite

	ld a,0b00000011		;pattern generator address
	ld b,4
	call vdp_RegWrite

	ld a,0b00110110		;sprite attribute table address
	ld b,5
	call vdp_RegWrite

	ld a,0b00000111		;sprite pattern table address
	ld b,6
	call vdp_RegWrite

	ld a,0b00000001		;setting screen colors
	ld b,7
	call vdp_RegWrite

	ret

;input: a = data
;	b = register number
vdp_RegWrite:
	set 7,b
	di
		out (VDP_REG),a
		ld a,b
		out (VDP_REG),a
	ei
	ret

;input: hl = RAM address
;	de = VRAM address
;	b = bytes to write
vdp_VRAMWrite:
	push bc
		set 6,d
		ld c,VDP_REG
		di
			out (c),e
			out (c),d
			dec c
			otir
		ei
	pop bc
	ret

;output: a = VDP status register
vdp_RegRead:
	di
		in a,(VDP_REG)
	ei
	ret

;input: hl = RAM address
;	de = VRAM address
;	b = number of bytes
vdp_VRAMRead:
	push bc
		ld c,VDP_REG
		di
			out (c),e
			out (c),d
			dec c
			inir
		ei
	pop bc
	ret
