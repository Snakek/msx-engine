;8x8 bit multiplication
;input: h = multiplier
;	e = multiplicand
;output: hl = product
;alters: l,d
math_Mult8:
    ld d,0
    ld l,d
    ld b,8
Mult8_Loop:
    add hl,hl
    jr nc,Mult8_NoAdd
    add hl,de
Mult8_NoAdd:
    djnz Mult8_Loop
    ret

;8x16 bit multiplication
;input: a = multiplicand
;	de = multiplier
;output: hl = product
math_Mult12:
    ld l,0
    ld b,8
Mult12_Loop:
    add hl,hl
    add a,a
    jr nc,Mult12_NoAdd
    add hl,de
Mult12_NoAdd:
    djnz Mult12_Loop
    ret

;16x16 multiplication
;input: bc = multiplier
;	de = multiplicand
;output: hl = product
;alters: a
math_Mult16:
    ld a,b
    ld b,16
Mult16_Loop:
    add hl,hl
    sla c
    rla
    jr nc,Mult16_NoAdd
    add hl,de
Mult16_NoAdd:
    djnz Mult16_Loop
    ret

;16x16 multiplication (32 bit output)
;input: bc = multiplicator 
;	de = multiplicand
;output: bchl = product
;alters: a
math_Mult32:
    ld a,c
    ld c,b
    ld hl,0
    ld b,16
Mult32_Loop:
    add hl,hl
    rla
    rl c
    jr nc,Mult32_NoAdd
    add hl,de
    adc a,0
    jp nc,Mult32_NoAdd
    inc c
Mult32_NoAdd:
    djnz Mult32_Loop
    ld b,c
    ld c,a
    ret

;8 bit division
;input: e = dividend
;	c = divisor
;output: a = quotient
;	 b = remainder
math_Div8:
	xor a
	ld b,8
Div8_Loop:
	rl e
	rla
	sub c
	jr nc,Div8_NoAdd
	add a,c
Div8_NoAdd:
	djnz Div8_Loop
	ld b,a
	ld a,e
	rla
	cpl
	ret

;Divide 16-bit values (with 16-bit result)
;input: bc = dividend
;	de = divisor
;output: bc = quotient
;	 hl = remainder
math_Div16:
	ld hl,0
	ld a,b
	ld b,8
Div16_Loop1:
	rla
	adc hl,hl
	sbc hl,de
	jr nc,Div16_NoAdd1
	add hl,de
Div16_NoAdd1:
	djnz Div16_Loop1
	rla
	cpl
	ld b,a
	ld a,c
	ld c,b
	ld b,8
Div16_Loop2:
	rla
	adc hl,hl
	sbc hl,de
	jr nc,Div16_NoAdd2
	add hl,de
Div16_NoAdd2:
	djnz Div16_Loop2
	rla
	cpl
	ld b,c
	ld c,a
	ret
